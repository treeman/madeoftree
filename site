#!/usr/bin/env perl

use autodie;
use Modern::Perl;
use Getopt::Long;
use Data::Dumper;
use File::Basename;
use POSIX qw/strftime/;
use File::Path qw(make_path);
use File::Find;
use threads;
use Test::More;

# Set website url
my $url = "www.madeoftree.se";
# Custom domain must have the same name as bucket
my $bucket = $url;

# Fetch path to current directory
my $dir = dirname(__FILE__);

# Fetch command line options
my $help;
my $debug;

my $update;
my $build;
my $sync;
my $host;

my $new;
my $edit;
my $wiki;
my $list_wiki;

my $link;

GetOptions(
    'help|h' => \$help,
    'debug' => \$debug,

    'update' => \$update,
    'build' => \$build,
    'sync' => \$sync,
    'host' => \$host,

    'new' => \$new,
    'edit' => \$edit,

    'wiki' => \$wiki,
    'wikis' => \$list_wiki,

    'link=s' => \$link,
);

sub get_clean_title {
    my $clean_title = lc (join ' ', @ARGV);
    $clean_title =~ s/\s/_/g;
    $clean_title =~ s/[^\w\d]//g;

    return $clean_title;
}

sub what_post_file {
    die "No title given" unless scalar @ARGV;

    my $now = strftime ('%F', gmtime);
    my $clean_title = get_clean_title ();

    return "$dir/_posts/$now-$clean_title.markdown";
}

# Generate a directory tree for files
sub make_dir_tree
{
    my ($file) = @_;

    # Remove file basename
    my @dirs = split '/', $file;
    my $base = pop @dirs;
    my $dir = join ('/', @dirs);

    make_path($dir);
}

sub help {
    say "Easy site script.";
    say "";
    say "   --update";
    say "       Build and sync site.";
    say "   --build";
    say "       Generate _site.";
    say "   --sync";
    say "       Sync site from beneath _site.";
    say "   --host";
    say "       Host with jekyll server.";
    say "   --wikis";
    say "       List all wiki pages.";
    say "   --wiki [page]";
    say "       Write wiki page.";
    say "   --new [post title]";
    say "       Start writing a new post.";
    say "   --link all|internal|external|unknown";
    say "       List links currently inside _site.";
    say "   --link test";
    say "       Check a 200 response from all internal links.";
    say "   --edit [post title]";
    say "       Edit a post.";
    say "";

    exit;
}

sub build {
    say "Building site...";

    system("jekyll --url $url 2>&1");
}

sub sync {
    say "Syncing...";

    # Sync _site
    system("s3cmd sync --acl-public --guess-mime-type --delete-removed $dir/_site/ s3://$bucket 2>&1");

    # Force correct filetype for css, guessing mime type doesn't appear to be working.
    for my $file (<$dir/_site/css/*>) {
        my ($name) = $file =~ m|_site/(.+)|;

        system("s3cmd put -m text/css --acl-public $file s3://$bucket/$name 2>&1");
    }
}

sub host {
    say "Running jekyll server";
    say "url: http://0.0.0.0:4000/";

    system ("jekyll --server --auto --url $url $dir/ $dir/_site 2>&1");
}

sub fetch_site_links {
    my @all_file_names;

    find sub {
        return if -d;
        push @all_file_names, $File::Find::name;
    }, "$dir/_site/";

    my %external;
    my %internal;
    my %unknown;

    for my $path ( @all_file_names ) {
        next unless $path =~ /\.html$/;

        #say $path;
        open my $fh, '<', $path;

        my $s = do { local $/ = <$fh>; };

        my @matches = $s =~ m|<a href="([^"]+)">|g;

        for my $m (@matches) {
            if ($m =~ m<^https?://|^www\.>) {
                $external{$m} = 1;
            }
            elsif ($m =~ m|^/|) {
                $internal{$m} = 1;
            }
            else {
                $unknown{$m} = 1;
            }
        }
    }

    return \%external, \%internal, \%unknown;
}

if ($help) {
    help();
}
elsif ($list_wiki) {
    say "All wiki pages:\n";

    my @all_file_names;

    find sub {
        return if -d;
        push @all_file_names, $File::Find::name;
    }, "$dir/wiki";

    for my $path ( sort @all_file_names ) {
        my ($name) = $path =~ m|wiki/(.+)/index\.[^/.]+$|;

        $name = "index" unless $name;

        say $name;
    }
}
elsif ($wiki) {
    die "No name given" unless scalar @ARGV;

    my $title = join ' ', @ARGV;
    my $clean_title = lc ($title);
    $clean_title =~ s/\s/_/g;
    $clean_title =~ s|[^\w\d/]||g;

    my $file = "$dir/wiki/$clean_title/index.markdown";
    # Main page
    $file = "$dir/wiki/index.markdown" if $title eq 'index';

    unless (-r $file) {
        make_dir_tree ($file);

        open my $fh, '>:encoding(UTF-8)', $file;

        say $fh "---";
        say $fh "layout: wiki";
        say $fh "title: " . join (' ', @ARGV);
        say $fh "---";
        say $fh "";
        say $fh "";

        close $fh;
    }

    exec "vim $file";
}
elsif ($edit) {
    die "No title given" unless scalar @ARGV;

    my $now = strftime ('%F', gmtime);
    my $clean_title = get_clean_title ();

    my @files = <_posts/*>;
    @files = grep(m|_posts/\d{4}-\d{2}-\d{2}-$clean_title\.markdown|, @files);

    die "Could not find post '$clean_title'" unless scalar @files;

    my $file = shift @files;

    say "File '$file'" if $debug;

    die "Can't read file: '$file'" unless -r $file;

    exec "vim $file";
}
elsif ($new) {
    my $file = what_post_file();

    say "File '$file'" if $debug;

    die "File exists: '$file'" if -r $file;

    open my $fh, '>:encoding(UTF-8)', $file;

    say $fh "---";
    say $fh "layout: post";
    say $fh "title: " . join (' ', @ARGV);
    say $fh "date: " . strftime ('%F %T', gmtime);
    say $fh "category: ";
    say $fh "tags: ";
    say $fh "---";
    say $fh "";
    say $fh "";

    close $fh;

    exec "vim $file";
}
elsif ($host) {
    host();
}
elsif ($update) {
    build();
    sync();
}
elsif ($build) {
    build();
}
elsif ($sync) {
    sync();
}
elsif ($link) {
    my ($ext, $int, $unk) = fetch_site_links();

    if ($link eq 'test') {
        system ("jekyll --server --auto --url $url $dir/ $dir/_site &");

        # Wait for server to correct itself
        sleep 5;

        my %fails;
        my @ugly;

        for my $url (keys %$int) {
            $url = "http://0.0.0.0:4000$url";
            $url = "$url/" unless $url =~ m</$|index\.html$>;

            push (@ugly, $url) if $url =~ /index.html$/;

            my $s = `curl -I -s $url`;

            if ($s =~ m|HTTP.*?\s+(\d+)|) {
                my $code = $1;

                $fails{$url} = $code unless $code == 200;
            }
        }

        sub pretty {
            my $url = shift;
            $url =~ s|^http://0.0.0.0:4000||;
            $url =~ s|/$||;
            return $url;
        }

        my $num = scalar keys %fails;
        say "$num fail urls.";
        for my $url (keys %fails) {
            say pretty ($url);
        }

        my $uglies = scalar @ugly;
        say "\n$uglies ugly urls.";
        for my $url (@ugly) {
            say pretty ($url);
        }

        # Forcefully remove jekyll
        `pkill jekyll`;
    }
    else {

        if ($link eq 'internal' || $link eq 'all') {
            say "# Internal links:\n";
            say for sort keys %$int;
        }

        if ($link eq 'external' || $link eq 'all') {
            say "\n# External links:\n";
            say for sort keys %$ext;
        }

        if ($link eq 'unknown' || $link eq 'all') {
            say "\n# Unknown links:\n";
            say for sort keys %$unk;
        }
    }
}
else {
    help();
}

